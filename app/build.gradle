plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
//    sourceSets.main.java.srcDirs += 'build/generated/source/fsb'
    sourceSets.main.kotlin.srcDirs += 'build/generated/source/fsb'
    compileSdk 32
    buildToolsVersion "32.1.0-rc1"
    useLibrary("android.test.base")
    useLibrary("android.test.mock")
    defaultConfig {
        applicationId "com.kuromelabs.kurome"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    composeOptions {
        kotlinCompilerExtensionVersion rootProject.compose_version
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    task generateFbsKotlin(type: Exec) {
        def inputDir = file("$projectDir/src/main/fbs")
        def outputKotlinDir = file("$projectDir/build/generated/source/fsb/")
        def fbsFiles = layout.files { file(inputDir).listFiles() }.filter { File f -> f.name.endsWith(".fbs") }.toList()
        ignoreExitValue(true)

        standardOutput = new ByteArrayOutputStream()
        errorOutput = new ByteArrayOutputStream()
        def commandLineArgs = ['flatc', '-o', outputKotlinDir, '--kotlin']
        fbsFiles.forEach{
            commandLineArgs.add(it.path)
        }
        commandLine commandLineArgs

        doFirst {
            delete "$outputKotlinDir/"
            mkdir "$outputKotlinDir/"
        }
        doLast {
            if (executionResult.get().exitValue != 0) {
                println(standardOutput.toString())
                throw new GradleException("flatc command line failed")
            }
        }
    }
    afterEvaluate {
        android.applicationVariants.all { variant ->
            variant.javaCompileProvider.get().dependsOn(generateFbsKotlin)
        }
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/fsb'
    }
}
dependencies {

    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    kapt 'androidx.room:room-compiler:2.4.2'
    implementation 'com.google.accompanist:accompanist-permissions:0.24.3-alpha'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.room:room-runtime:2.4.2'
    implementation 'androidx.room:room-ktx:2.4.2'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.flatbuffers:flatbuffers-java:2.0.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'


    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'androidx.test:core:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Compose
    implementation "androidx.compose.runtime:runtime:$rootProject.compose_version"
    implementation "androidx.compose.ui:ui:$rootProject.compose_version"
    implementation "androidx.compose.foundation:foundation:$rootProject.compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$rootProject.compose_version"
    implementation "androidx.compose.material:material:$rootProject.compose_version"
    implementation "androidx.compose.ui:ui-tooling:$rootProject.compose_version"
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.compose.ui:ui-tooling-preview:$rootProject.compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$rootProject.compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$rootProject.compose_version"

}